plugins {
    id 'java'
    id "xyz.jpenilla.run-paper" version "2.0.0"
    id "org.sonarqube" version "3.5.0.2730"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'eu.fx3.plugins'
version = '1.8.1-SNAPSHOT'
def mainPackage = "eu.fx3.plugins.totaldeathmessages"

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }
}

dependencies {
    // Only for development, not needed for runtime
    compileOnly 'org.jetbrains:annotations:23.0.0'

    // Provided by PaperMC server itself
    shadow 'io.papermc.paper:paper-api:1.19.2-R0.1-SNAPSHOT'
    compileOnly 'net.kyori:adventure-text-minimessage:4.12.0'

    // Provided by server via the libraries entry in plugin.yml
    compileOnly 'org.apache.commons:commons-text:1.10.0'

    // Must be included, shadowed and relocated
    implementation 'com.github.simplix-softworks:simplixstorage:3.2.5'
    implementation 'net.kyori:adventure-text-serializer-bungeecord:4.2.0'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks {
    runServer {
        minecraftVersion("1.19.2")
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "mindsolve_TotalDeathMessages"
        property "sonar.organization", "mindsolve"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

shadowJar {
    dependencies {
        // Only shadow these dependencies
        include dependency('net.kyori:adventure-text-serializer-bungeecord:')
        include dependency('com.github.simplix-softworks:simplixstorage:')
    }

    relocate 'net.kyori.adventure.text.serializer.bungeecord', mainPackage + '.shadow.adventure-bungeecord'
    relocate 'de.leonhard.storage', mainPackage + '.shadow.simplixstorage'
}